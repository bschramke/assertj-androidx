subprojects {
  buildscript {
    repositories {
      google()
      jcenter()
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:3.3.2'
      classpath 'com.novoda:bintray-release:0.9'
    }
  }

  repositories {
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  minSdkVersion = 14
  compileSdkVersion = 28
  buildToolsVersion = '28.0.3'

  assertj = 'org.assertj:assertj-core:1.7.1'
  androidxAnnotation = 'androidx.annotation:annotation:1.0.0'
  androidxSupportV4 = 'androidx.legacy:legacy-support-v4:1.0.0'
  androidxAppCompat = 'androidx.appcompat:appcompat:1.0.0'
  androidxCardView = 'androidx.cardview:cardview:1.0.0'
  androidxMaterial = 'com.google.android.material:material:1.0.0-rc01'
  androidxGridLayout = 'androidx.gridlayout:gridlayout:1.0.0'
  androidxMediaRouter = 'androidx.mediarouter:mediarouter:1.0.0'
  androidxPalette = 'androidx.palette:palette:1.0.0'
  androidxRecyclerView = 'androidx.recyclerview:recyclerview:1.0.0'

  playServices = 'com.google.android.gms:play-services:7.5.0'
}

subprojects {
  afterEvaluate {
    android.libraryVariants.all { variant ->
      def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
      task.description = "Create jar artifact for ${variant.name}"
      task.dependsOn variant.javaCompile
      task.from variant.javaCompile.destinationDir
      task.destinationDir = project.file("${project.buildDir}/outputs/jar")
      task.archiveName = "${project.name}-${variant.baseName}.jar"
      artifacts.add('archives', task)
    }
  }
}
