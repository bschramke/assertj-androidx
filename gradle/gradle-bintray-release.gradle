if (!project.rootProject.file('local.properties').exists()) {
  println "Not applying gradle-bintray-release.gradle"
  return
}
apply plugin: 'com.novoda.bintray-release'

def getBintrayUserAndKey() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return [
      properties.getProperty("bintray.user"),
      properties.getProperty("bintray.apikey")
  ]
}

if (POM_ARTIFACT_ID == null) {
  throw new IllegalStateException("Must specify a shard in module's build.gradle")
} else if (VERSION_NAME == null || VERSION_NAME.length() == 0) {
  throw new IllegalStateException("Unable to reference VERSION_NAME")
}

task checkBintrayConfig {
  doLast {
    def (user, key) = getBintrayUserAndKey()
    if (user == null || user.isEmpty() ||
        key == null || key.isEmpty()) {
      throw new IllegalStateException("Must specify Bintray user/API key in your local.properties.")
    }
  }
}

afterEvaluate {
  bintrayUpload.dependsOn checkBintrayConfig
}

def (user, key) = getBintrayUserAndKey()
publish {
  bintrayUser = user
  bintrayKey = key
  userOrg = 'bschramke'
  groupId = GROUP
  uploadName = "assertj-androidx:$POM_ARTIFACT_ID"
  artifactId = POM_ARTIFACT_ID
  publishVersion = VERSION_NAME
  desc = POM_DESCRIPTION
  website = 'https://github.com/bschramke/assertj-androidx'
  dryRun = false
}